org 100h
S equ 1193180 ; Тактовая частота синхронизации

beep_proc:
  mov al, 10110110b ; Управляющее слово
  out 43h, al ; Записываем управляющее слово в порт 43h

  mov bx, mas1 ; Указатель на массив с нотами
  mov di, mas2 ; Указатель на массив с длительностями нот	

; Включение динамика
beep_start:
  in al, 61h
  or al, 00000011b
  out 61h, al

  mov cx, 65 ; Количество нот 

start:
  push cx ; Сохраняем значение счетчика
  mov ax, [bx]
  shr ax, 1 ; Логический сдвиг вправо на 1 бит для увеличения частоты нот
  out 42h, al ; Записываем младший байт делителя частоты
  mov al, ah
  out 42h, al ; Записываем старший байт делителя частоты
  mov cx, [di+2] ; Загружаем размер задержки
  mov dx, [di]
  shl cx, 1 ; Логический сдвиг влево на 1 бит для увеличения длины ноты
  mov ah, 86h ; Функция 86 использует регистры cx и dx на входе для формирования задержки
  int 15h ; Осуществляем задержку
  add bx, 2 ; Смещаем указатель на ноты
  add di, 4 ; Смещаем указатель на длительности нот
  pop cx ; Восстанавливаем исходное значение счетчика
  loop start

; Выключение динамика после воспроизведения второй части
beep_end:
  in al, 61h
  and al, 11111100b
  out 61h, al

  int 20h ; Завершаем программу

; Массив с частотами нот 
mas1:
   dw S/493, S/493, S/523, S/587, S/587, S/523, S/493, S/440, S/392, S/392, S/440, S/493, S/493, S/440, S/440, 1
   dw S/493, S/493, S/523, S/587, S/587, S/523, S/493, S/440, S/392, S/392, S/440, S/493, S/440, S/392, S/392, 1
   dw S/440, S/440, S/493, S/392, S/440, S/493, S/523, S/493, S/392, S/440, S/493, S/523, S/493, S/440, S/392, S/440, S/293, 1
   dw S/493, S/493, S/523, S/587, S/587, S/523, S/493, S/440, S/392, S/392, S/440, S/493, S/440, S/392, S/392, 1

; Массив с длинами нот 
mas2:
   dd 3D090h, 3D090h, 3D090h, 3D090h, 3D090h , 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h +1E848h, 1E848h, 7A120h, 1E848h/7
   dd 3D090h, 3D090h , 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h , 3D090h, 3D090h + 1E848h, 1E848h, 7A120h, 1E848h/7
   dd 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 1E848h, 1E848h, 3D090h, 3D090h, 3D090h, 3D090h, 7A120h, 1E848h/7
   dd 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h, 3D090h + 1E848h, 1E848h, 0F4240h, 64h    
